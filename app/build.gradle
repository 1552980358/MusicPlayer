apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'AndResGuard'

andResGuard {
    // mappingFile = file("./resource_mapping.txt")
    mappingFile = null
    use7zip = true
    useSign = true
    // 打开这个开关，会keep住所有资源的原始路径，只混淆资源的名字
    keepRoot = false
    // 设置这个值，会把arsc name列混淆成相同的名字，减少string常量池的大小
    fixedResName = "arg"
    // 打开这个开关会合并所有哈希值相同的资源，但请不要过度依赖这个功能去除去冗余资源
    mergeDuplicatedRes = true
    whiteList = [
    ]
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif"
    ]
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.2.17'
        //path = "/usr/local/bin/7za"
    }
}

def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

//noinspection GroovyMissingReturnStatement
android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "app.skynight.musicplayer"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode gitCommits
        versionName "初见@${gitCommits}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    //noinspection GroovyMissingReturnStatement
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    sourceSets {
        main {
            jni.srcDirs = [] //disable automatic ndk-build
            jniLibs.srcDirs = ['src/main/libs']
            res.srcDirs =
                    [
                            'src/main/noti',
                            'src/main/base',
                            'src/main/view',
                            'src/main/main',
                            'src/main/intro',
                            'src/main/player',
                            'src/main/application'
                    ]
        }
    }
}

dependencies {
    def androidX = "1.1.0-rc01"
    def tinyPinYin = '2.0.3'

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.squareup.okhttp3:okhttp:4.1.1"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.github.mkaflowski:Media-Style-Palette:1.3'
    implementation 'com.github.bogerchan:Nier-Visualizer:v0.1.3'
    implementation "com.github.promeg:tinypinyin:${tinyPinYin}"
    implementation "com.github.promeg:tinypinyin-lexicons-android-cncity:${tinyPinYin}"
    implementation "androidx.preference:preference:1.1.0"
    implementation "androidx.preference:preference-ktx:1.1.0"
    implementation "androidx.appcompat:appcompat:${androidX}"
    implementation "androidx.core:core-ktx:1.2.0-alpha04"
    implementation "androidx.preference:preference:${androidX}"
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
